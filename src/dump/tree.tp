
;
;   TvmJIT : <http://github.com/fperrad/tvmjit/>
;   Copyright (C) 2013 Francois Perrad.
;

(!let error error)
(!let pairs pairs)
(!let setmetatable setmetatable)
(!let tostring tostring)
(!let type type)
(!let quote (!index tvm "quote"))
(!let tconcat (!index table "concat"))


(!let dumpers (!call setmetatable () ("__index": (!lambda (t k)
                (!call error (!mconcat "dump '" k "' is unimplemented"))))))

(!assign (!index dumpers "nil") (!lambda (buffer)
                (!assign (!index buffer (!add (!len buffer) 1)) "!nil")))

(!assign (!index dumpers "boolean") (!lambda (buffer bool)
                (!if bool
                     (!assign (!index buffer (!add (!len buffer) 1)) "!true")
                     (!assign (!index buffer (!add (!len buffer) 1)) "!false"))))

(!assign (!index dumpers "number") (!lambda (buffer num)
                (!cond ((!ne num num)           (!assign (!index buffer (!add (!len buffer) 1)) "(!div 0 0)"))
                       ((!eq num (!div 1 0))    (!assign (!index buffer (!add (!len buffer) 1)) "(!div 1 0)"))
                       ((!eq num (!div -1 0))   (!assign (!index buffer (!add (!len buffer) 1)) "(!div -1 0)"))
                       (!true                   (!assign (!index buffer (!add (!len buffer) 1)) (!call tostring num))))))

(!assign (!index dumpers "string") (!lambda (buffer str)
                (!assign (!index buffer (!add (!len buffer) 1)) (!call quote str))))

(!assign (!index dumpers "table") (!lambda (buffer tbl seen)
                (!if (!index seen tbl)
                     (!call error "cycle detected"))
                (!assign (!index seen tbl) !true)
                (!define (is_map n max) (!false 0 1))
                (!for (k) ((!call pairs tbl))
                        (!if (!and (!eq (!call1 type k) "number") (!ge k 1))
                             (!if (!gt k max)
                                  (!assign max k))
                             (!assign is_map !true))
                        (!assign n (!add n 1)))
                (!if (!gt max (!mul 2 n))       ; sparse array
                     (!assign is_map !true))
                (!assign (!index buffer (!add (!len buffer) 1)) "(")
                (!define not_first !false)
                (!if is_map
                     (!for (k v) ((!call pairs tbl))
                            (!if not_first
                                 (!assign (!index buffer (!add (!len buffer) 1)) " "))
                            (!call (!index dumpers (!call1 type k)) buffer k seen)
                            (!assign (!index buffer (!add (!len buffer) 1)) ":")
                            (!call (!index dumpers (!call1 type v)) buffer v seen)
                            (!assign (!index buffer (!add (!len buffer) 1)) " ")
                            (!assign not_first !true))
                     (!loop i 1 max 1
                            (!if not_first
                                 (!assign (!index buffer (!add (!len buffer) 1)) " "))
                            (!let v (!index tbl i))
                            (!call (!index dumpers (!call1 type v)) buffer v seen)
                            (!assign (!index buffer (!add (!len buffer) 1)) " ")
                            (!assign not_first !true)))
                (!assign (!index buffer (!add (!len buffer) 1)) ")")))

(!return (!lambda (data)
                (!let buffer ("(!return "))
                (!call (!index dumpers (!call1 type data)) buffer data ())
                (!assign (!index buffer (!add (!len buffer) 1)) ")")
                (!return (!call tconcat buffer))))
